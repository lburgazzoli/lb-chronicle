// *****************************************************************************
// -Djava.util.logging.manager=org.apache.logging.log4j.jul.LogManager
// *****************************************************************************

buildscript {
    repositories {
        mavenCentral()
        mavenLocal()
        jcenter()
    }

    dependencies {
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.11.3'
    }
}

// *****************************************************************************
//
// *****************************************************************************

allprojects {
    configurations {
        all {
            resolutionStrategy {
                componentSelection {
                    all { ComponentSelection selection ->
                        boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm'].any {
                            qualifier -> selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
                        }

                        if (rejected) {
                            selection.reject('Release candidate')
                        }
                    }
                }
            }
        }
    }
}

// *****************************************************************************
//
// *****************************************************************************

subprojects {
    apply plugin: 'java'
    apply plugin: 'groovy'
    apply plugin: 'maven'
    apply plugin: 'idea'
    apply plugin: 'com.github.ben-manes.versions'

    version             = '1.0.0-SNAPSHOT'
    group               = 'com.github.lburgazzoli'
    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'
  
    ext {
        versions = [
            'slf4j'                  : '1.7.12',
            'openhftChronicleQueue'  : '3.4.4',
            'openhftChronicleLogger' : '1.1.0',
            'joptSimple'             : '4.8-beta-1',
            'junit'                  : '4.12',
            'commonsLang'            : '3.4',
            'groovy'                 : '2.4.3'
        ]

        gradleVersion    = '2.4'
        gradleScriptDir  = "${rootProject.projectDir}/gradle"
        prefix           = 'lb'
    }
   
    repositories {
        mavenLocal()
        mavenCentral()

        maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
    }

    dependencies {
        compile("org.slf4j:slf4j-api:$versions.slf4j") { transitive = false }
        compile("org.slf4j:slf4j-ext:$versions.slf4j") { transitive = false }
        compile("org.slf4j:jul-to-slf4j:$versions.slf4j") { transitive = false }
        compile("org.slf4j:jcl-over-slf4j:$versions.slf4j") { transitive = false }
        compile("org.slf4j:jcl-over-slf4j:$versions.slf4j") { transitive = false }

        compile "org.codehaus.groovy:groovy:$versions.groovy:indy"
        compile "org.apache.commons:commons-lang3:$versions.commonsLang"
        
        testCompile "junit:junit:$versions.junit"
    }

    test {
        testLogging {
            events          "failed"
            exceptionFormat "full"
        }
    }

    idea {
        module {
            downloadJavadoc = true
            downloadSources = true
        }
    }

    [compileJava, compileTestJava]*.options*.compilerArgs = [
        "-Xlint:unchecked",
        "-Xlint:deprecation",
    ]

    dependencyUpdates {
        revision = 'release'
    }
}

// *****************************************************************************
//
// *****************************************************************************

task wrapper(type: Wrapper) {
    gradleVersion = "$gradleVersion"
}
